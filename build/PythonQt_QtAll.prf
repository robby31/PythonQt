# profile for non-mevis users to link to PythonQt_QtAll

INCLUDEPATH += $$PWD/../extensions/PythonQt_QtAll

# check if debug or release
CONFIG(debug, debug|release) {
  DEBUG_EXT = _d 
} else {
  DEBUG_EXT = 
}

!exists($$(MYLIBRARY)) {
    error("variable MYLIBRARY not set.")
}

win32-msvc* {
  LIBS += $$(MYLIBRARY)/$$QT_VERSION/PythonQt_QtAll$${DEBUG_EXT}.lib
} else {
  LIBS += -L$$(MYLIBRARY)/$$QT_VERSION -lPythonQt_QtAll$${DEBUG_EXT}
}


# statically link PythonQt?
PYTHONQT_STATIC = $$(PYTHONQT_STATIC)
isEmpty(PYTHONQT_STATIC) {
} else {
  # static linking
  QT += gui svg sql network xml opengl

  lessThan(QT_MAJOR_VERSION, 5) {
    QT += webkit
  } else {
    lessThan(QT_MINOR_VERSION, 6) {
      QT += webkit
    }
  }

  contains(QT_MAJOR_VERSION, 5) {
    QT += widgets
    QT += uitools
    lessThan(QT_MINOR_VERSION, 6) {
      QT += webkitwidgets
    } else {
      DEFINES += QT_NO_WEBKIT
    }
  } else {
    CONFIG += uitools
  }
}
 
